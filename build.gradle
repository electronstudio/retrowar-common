apply plugin: "kotlin"
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

sourceCompatibility = 1.6
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets.main.java.srcDirs = ["src/"]
sourceSets.main.resources.srcDirs = ["resources/"]


repositories {
    flatDir {
        dirs '../libs'
    }
}

configurations {
    ktlint
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

dependencies {
    compile 'io.sentry:sentry:1.7.3'
    compile 'org.slf4j:slf4j-simple:1.7.21'
    compile "com.code-disaster.steamworks4j:steamworks4j:1.6.2"
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile "net.dermetfan.libgdx-utils:libgdx-utils:$gdxUtilsVersion"
    compile "net.mostlyoriginal.artemis-odb:contrib-core:2.2.0"
    compile "net.mostlyoriginal.artemis-odb:contrib-eventbus:2.2.0"
    compile "net.mostlyoriginal.artemis-odb:contrib-plugin-profiler:2.2.0"
    ktlint 'com.github.shyiko:ktlint:0.30.0'
    compile "net.onedaybeard.artemis:artemis-odb:2.1.0"
    compile "net.onedaybeard.artemis:artemis-odb-serializer-kryo:2.1.0"
    compile("com.esotericsoftware:kryo:4.0.2")
    compile("com.esotericsoftware:kryonet:2.22.0-RC1") {
        exclude module: 'kryo'
    }
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    //compile "com.badlogicgames.gdx:gdx-controllers:$gdxVersion"
   compile "uk.co.electronstudio.sdl2gdx:sdl2gdx:1.0.1-jimbly"

    compile "com.badlogicgames.ashley:ashley:$ashleyVersion"
    compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    compile 'com.beust:klaxon:0.30'
    // https://mvnrepository.com/artifact/org.codehaus.groovy/groovy-all
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.6'
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'

}

apply plugin: 'org.jetbrains.dokka'

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    includeNonPublic = false
    skipEmptyPackages = true
    jdkVersion = 8

    externalDocumentationLink {
        // Root URL of the generated documentation to link with. The trailing slash is required!
        url = new URL("https://libgdx.badlogicgames.com/ci/nightlies/docs/api/")
        packageListUrl = new URL("https://libgdx.badlogicgames.com/ci/nightlies/docs/api/package-list")
    }

    packageOptions {
        prefix = "uk.co.electronstudio.retrowar.music"
        suppress = true
    }
    packageOptions {
        prefix = "uk.co.electronstudio.retrowar.menu"
        suppress = true
    }

    packageOptions {
        prefix = "uk.co.electronstudio.retrowar.network"
        suppress = true
    }
    packageOptions {
        prefix = "de.golfgl.gdxgameanalytics"
        suppress = true
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

jar {
    manifest {
        attributes 'Implementation-Version': version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives sourcesJar
    archives javadocJar
}




def pomConfig = {
    licenses {
        license {
            name "Apache License version 2"
            url "https://apache.org/licenses/LICENSE-2.0"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "electronstudio"
            name "Electron Studio"
            email "support@electronstudio.co.uk"
        }
    }

    scm {
        url "https://github.com/electronstudio/"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'uk.co.electronstudio.retrowar'
            artifactId 'retrowar-common'
            version version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'LibGDX extension library')
                root.appendNode('name', 'retrowar-common')
                root.appendNode('url', 'https://github.com/electronstudio/retrowar-common')
                root.children().last() + pomConfig
            }
        }
    }
  publish.dependsOn jar, sourcesJar, javadocJar, generatePomFileForMavenPublicationPublication
}

String keyString = ''
File file = new File(System.getProperty("user.home")+"/bintray.key")
if(file.exists()){
    keyString = file.text.trim()
}
bintray {
    user = 'electronstudio' //System.getenv('BINTRAY_USER')
    key = keyString //System.getenv('BINTRAY_KEY')
    publications = ['mavenPublication']
    publish = true
    override = true
  pkg {
        repo = 'retrowar'
        name = 'retrowar-common'
     //   userOrg = 'electronstudio'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/electronstudio/retrowar-common.git'
      version {
          name = '0.19.2'
          desc = '0.19.2 description'
          released = new Date()
          vcsTag = '0.19.2'
      }
    }
}

bintrayUpload.dependsOn publish
